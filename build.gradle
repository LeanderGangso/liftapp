import io.gitlab.arturbosch.detekt.Detekt

buildscript {

    apply from: "gradle/versions.gradle"
    apply from: "gradle/dependencies.gradle"

    dependencies {

        classpath "com.google.dagger:hilt-android-gradle-plugin:2.43.2"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.google.android.gms:oss-licenses-plugin:$versions.ossLicensesPlugin"
    }

    repositories {

        gradlePluginPortal()
    }
}

plugins {

    id "com.android.application" version '7.4.0' apply false
    id "com.android.library" version '7.4.0' apply false
    id "org.jetbrains.kotlin.android" version "$kotlin_version" apply false
    id "com.google.devtools.ksp" version "$kotlin_version-1.0.8" apply false
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version" apply false
    id "io.gitlab.arturbosch.detekt" version "1.21.0"
}

dependencies {

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.21.0"
}

detekt {

    buildUponDefaultConfig = true
    source = files("app", "core", "domain", "feature", "functionality")
    config = files(".idea/detekt-config.yml")
}

tasks.withType(Detekt).configureEach {

    jvmTarget = "11"

    reports {

        html {

            enabled = true
            destination = file("build/reports/detekt/detekt.html")
        }
    }
}

task detektFix(type: Detekt) {

    autoCorrect = true
    buildUponDefaultConfig = true
    source = files("app", "core", "domain", "feature", "functionality")
    config.setFrom(files(".idea/detekt-config.yml"))
}

task clean(type: Delete) {

    delete rootProject.buildDir
}
